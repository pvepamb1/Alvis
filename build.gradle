buildscript {
	ext {
		springBootVersion = '2.1.6.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.automation'
version = '0.6.5'
sourceCompatibility = 1.11

repositories {
	mavenCentral()
}

bootJar {
	launchScript()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-mail')
	compile('javax.xml.bind:jaxb-api')
	runtime('mysql:mysql-connector-java')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	compileOnly "org.projectlombok:lombok:1.18.8"
	annotationProcessor "org.projectlombok:lombok:1.18.8"
}

task debianClean(type: Delete) {
	group = 'debian'
	description = 'deletes build directory'
	delete 'build/'	
}

task buildTar(type: Tar){
	
	group = 'debian'
	description = 'creates a tar'
	
	compression = Compression.GZIP
	archiveName = "homeauto_${version}.orig.tar.gz"
	destinationDir = file("build/debian/")
	from "build/libs/", "gradle/service"

}

task replaceWords(type: Copy) {

	group = 'debian'
	description = 'replaces words'
	
    from "gradle/debian/changelog"
    into "build/tmp/"
    filter { line -> line.replaceAll('xxx', version) }
}

task ppaScripts(type: Exec) {
    commandLine "sh", "./gradle/script/debuild-signed.sh"
    args "homeauto-${version}"
    
}

task listDir(type: Exec){

	commandLine "ls", "build/"
}

task debianPrepare() { 
	
	group = 'debian'
	description = 'assembles a debian source package folder'
	
	doLast {
	
		copy {
			from "gradle/debian", "build/tmp/changelog"
			into "build/debian/homeauto-${version}/debian"
		}

		copy{
			from "build/libs", "gradle/service"
			into "build/debian/homeauto-${version}/"
		}

	}
}

